var PI = 3.141592653589793;
var SOLAR_MASS = PI*PI*4;
var DAYS_PER_YEAR = 365.24;

obj Body {
    def create()
    {
        far x, y, z, xv, yv, zv, mass;
    }
}

def make_body(x, y, z, xv, yv, zv, mass)
{
    var body = instance_create(Body);
    body.x = x;
    body.y = y;
    body.z = z;
    body.xv = xv;
    body.yv = yv;
    body.zv = zv;
    body.mass = mass;
    return body;
}

globalvar bodies = [];

// sun
global.bodies->push(make_body(
    0,
    0,
    0,
    0,
    0,
    0,
    SOLAR_MASS
));
// jupiter
global.bodies->push(make_body(
     4.84143144246472090e+00,
    -1.16032004402742839e+00,
    -1.03622044471123109e-01,
     1.66007664274403694e-03 * DAYS_PER_YEAR,
     7.69901118419740425e-03 * DAYS_PER_YEAR,
    -6.90460016972063023e-05 * DAYS_PER_YEAR,
     9.54791938424326609e-04 * SOLAR_MASS
));
// saturn
global.bodies->push(make_body(
     8.34336671824457987e+00,
     4.12479856412430479e+00,
    -4.03523417114321381e-01,
    -2.76742510726862411e-03 * DAYS_PER_YEAR,
     4.99852801234917238e-03 * DAYS_PER_YEAR,
     2.30417297573763929e-05 * DAYS_PER_YEAR,
     2.85885980666130812e-04 * SOLAR_MASS
));
// uranus
global.bodies->push(make_body(
     1.28943695621391310e+01,
    -1.51111514016986312e+01,
    -2.23307578892655734e-01,
     2.96460137564761618e-03 * DAYS_PER_YEAR,
     2.37847173959480950e-03 * DAYS_PER_YEAR,
    -2.96589568540237556e-05 * DAYS_PER_YEAR,
     4.36624404335156298e-05 * SOLAR_MASS
));
// neptune
global.bodies->push(make_body(
     1.53796971148509165e+01,
    -2.59193146099879641e+01,
     1.79258772950371181e-01,
     2.68067772490389322e-03 * DAYS_PER_YEAR,
     1.62824170038242295e-03 * DAYS_PER_YEAR,
    -9.51592254519715870e-05 * DAYS_PER_YEAR,
     5.15138902046611451e-05 * SOLAR_MASS
));

globalvar num_bodies = global.bodies->len();

subdef advance(delta)
{
    for(var i = 0; i < global.num_bodies; i++)
    {
        for(var j = i+1; j < global.num_bodies; j++)
        {
            var dx = global.bodies[i].x - global.bodies[j].x;
            var dy = global.bodies[i].y - global.bodies[j].y;
            var dz = global.bodies[i].z - global.bodies[j].z;
            var distance_sq = dx*dx + dy*dy + dz*dz;
            var distance = sqrt(distance_sq);
            var power = delta / distance_sq / distance;
            var mass_i = global.bodies[i].mass;
            var mass_j = global.bodies[j].mass;
            global.bodies[i].xv -= power * dx * mass_j;
            global.bodies[i].yv -= power * dy * mass_j;
            global.bodies[i].zv -= power * dz * mass_j;
            global.bodies[j].xv += power * dx * mass_i;
            global.bodies[j].yv += power * dy * mass_i;
            global.bodies[j].zv += power * dz * mass_i;
        }
    }
    for(var i = 0; i < global.num_bodies; i++)
    {
        global.bodies[i].x += global.bodies[i].xv * delta;
        global.bodies[i].y += global.bodies[i].yv * delta;
        global.bodies[i].z += global.bodies[i].zv * delta;
    }
}
subdef energy()
{
    var e = 0;
    for(var i = 0; i < global.num_bodies; i++)
    {
        var body_a = global.bodies[i];
        e += ( body_a.mass
             * ( body_a.xv * body_a.xv
               + body_a.yv * body_a.yv
               + body_a.zv * body_a.zv
               )
             ) / 2;
        for(var j = i+1; j < global.num_bodies; j++)
        {
            var body_b = global.bodies[j];
            var dx = body_a.x - body_b.x;
            var dy = body_a.y - body_b.y;
            var dz = body_a.z - body_b.z;
            var distance = sqrt(dx*dx + dy*dy + dz*dz);
            e -= (body_a.mass * body_b.mass)/distance;
        }
    }
    return e;
}
subdef offset_momentum()
{
    var px = 0, py = 0, pz = 0;
    for(var i = 0; i < global.num_bodies; i++)
    {
        var body_a = global.bodies[i];
        px += body_a.xv * body_a.mass;
        py += body_a.yv * body_a.mass;
        pz += body_a.zv * body_a.mass;
    }
    global.bodies[0].xv = -px/SOLAR_MASS;
    global.bodies[0].yv = -py/SOLAR_MASS;
    global.bodies[0].zv = -pz/SOLAR_MASS;
}

print(global.bodies);
offset_momentum();
print(energy());
for(var i = 0; i < 5000; i++)
    advance(0.01);
print(energy());
